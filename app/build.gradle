import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 5

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    signingConfigs {
        release {
            storeFile file('../keys/keys.jks')
            storePassword System.getenv("FULLDIVE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("FULLDIVE_ALIAS")
            keyPassword System.getenv("FULLDIVE_ALIAS_PASSWORD")
        }
    }

    defaultConfig {
        applicationId "com.fulldive.extension.eyefilter"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode versionMajor*10000+versionMinor*100+versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFileName = "ScreenDim-v${android.defaultConfig.versionName}-${variant.buildType.name}.apk"
                }
            }
        }

        debug {
            def repo = Grgit.open(currentDir: project.rootDir)
            def commitId = "${repo.log().get(0).getAbbreviatedId()}"
            if (repo.status().isClean()) {
                versionNameSuffix "-$commitId"
            } else {
                versionNameSuffix "-$commitId (dirty)"
            }
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        disable 'NewApi','ExpiredTargetSdkVersion'
    }
}

dependencies {
    // Android/platform deps
    implementation 'androidx.core:core:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Written by me
    implementation "me.smichel.android:kpreferences:0.7.0"
    implementation project(":timepickerpreference")

    // 3rd party
    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    implementation 'com.github.paolorotolo:appintro:4.1.0'
    implementation 'org.greenrobot:eventbus:3.2.0'
    // annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    implementation "com.snore.guard:screendim:1.3.0"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation("com.squareup.okhttp3:okhttp:4.9.1")
}

repositories {
    mavenCentral()
    maven { url "http://office.fulldive.com:8083" }
}
